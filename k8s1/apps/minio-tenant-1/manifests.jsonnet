local lib_hash2 = (import '../../../jsonnetlib/hash2.libsonnet');

local name = (import '_app_config.json').name;
local namespace = (import '_app_config.json').namespace;

local minio_user0_secret = lib_hash2 { data: {
  apiVersion: 'external-secrets.io/v1',
  kind: 'ExternalSecret',
  metadata: {
    name: name + '-user0-secret',
  },
  spec: {
    secretStoreRef: {
      kind: 'ClusterSecretStore',
      name: (import '../external-secrets/secret_store.jsonnet').metadata.name,
    },
    target: {
      template: {
        engineVersion: 'v2',
        data: {
          'CONSOLE_ACCESS_KEY': 'loki-logs-user',
          'CONSOLE_SECRET_KEY': '{{ .users0_secret_key }}',
        },
      },
    },
    data: [
      // https://start.1password.com/open/i?a=UWWKBI7TBZCR7JIGGPATTRJZPQ&v=tsa4qdut6lvgsrl5xvsvdnmgwe&i=ovkly32j3sw3on4qfs5uyp4aei&h=my.1password.com
      {
        secretKey: 'users0_secret_key',
        remoteRef: {
          key: 'ovkly32j3sw3on4qfs5uyp4aei/6xvsukjqnua2qmfhfzrdx65fpa/2v3ydlsmkkalx562zcabwogbeq',
        },
      }
    ]
  },
} }.output;

local minio_configuration = lib_hash2 { data: {
  apiVersion: 'external-secrets.io/v1',
  kind: 'ExternalSecret',
  metadata: {
    name: name + '-ex-secret',
  },
  spec: {
    secretStoreRef: {
      kind: 'ClusterSecretStore',
      name: (import '../../apps/external-secrets/secret_store.jsonnet').metadata.name,
    },
    target: {
      template: {
        engineVersion: 'v2',
        data: {
          // https://github.com/minio/operator/blob/e054c34ee36535b1323337816450dd7b3fcac482/helm/tenant/values.yaml#L75-L122
          // https://github.com/minio/operator/blob/e054c34ee36535b1323337816450dd7b3fcac482/helm/tenant/templates/tenant-configuration.yaml#L16-L18
          'config.env': |||
            export MINIO_ROOT_USER={{ .rootUser }}
            export MINIO_ROOT_PASSWORD={{ .rootPassword }}
          |||,
        },
      }
    },
    data: [
      // https://start.1password.com/open/i?a=UWWKBI7TBZCR7JIGGPATTRJZPQ&v=tsa4qdut6lvgsrl5xvsvdnmgwe&i=ovkly32j3sw3on4qfs5uyp4aei&h=my.1password.com
      {
        secretKey: 'rootUser',
        remoteRef: {
          key: 'ovkly32j3sw3on4qfs5uyp4aei/6xvsukjqnua2qmfhfzrdx65fpa/gcl7whpcuvza5x5mjc6v2og6fm',
        },
      },
      {
        secretKey: 'rootPassword',
        remoteRef: {
          key: 'ovkly32j3sw3on4qfs5uyp4aei/6xvsukjqnua2qmfhfzrdx65fpa/acyadgpgfgsc6oao6otgi6gacu',
        },
      },
    ],
  },
} }.output;

local node_affinity = {
  requiredDuringSchedulingIgnoredDuringExecution: {
    nodeSelectorTerms: [
      {
        matchExpressions: [
          {
            // nodes having disk allocation capability
            key: 'storage.longhorn.pollenjp.com/enabled',
            operator: 'In',
            values: [
              'true'
            ]
          }
        ]
      }
    ]
  }
};

local tenant = {
  apiVersion: 'minio.min.io/v2',
  kind: 'Tenant',
  metadata: {
    name: name,
    namespace: namespace,
  },
  spec: { // https://docs.min.io/community/minio-object-store/reference/operator-crd.html#tenant
    pools: [
      {
        // https://docs.min.io/community/minio-object-store/reference/operator-crd.html#pool
        name: name + 'pool-1',
        servers: 2,
        volumesPerServer: 2,
        volumeClaimTemplate: {
          // https://github.com/minio/operator/blob/master/docs/expansion.md#getting-started
          // https://github.com/minio/operator/blob/e054c34ee36535b1323337816450dd7b3fcac482/docs/expansion.md#getting-started
          metadata: {
            name: 'data',
          },
          spec: {
            accessModes: [
              'ReadWriteOnce',
            ],
            resources: {
              requests: {
                storage: '10Gi',
              },
            },
            // storageClassName: '',
          },
        },
        affinity: {
          nodeAffinity: node_affinity,
        },
      }
    ],
    // cpuv1
    // https://github.com/minio/minio/blob/b4b3d208dd7dad1ac67ce662412b89b0d70d68b6/helm/minio/values.yaml#L13-L29
    // repository: 'quay.io/minio/minio',
    image: 'quay.io/minio/minio:RELEASE.2025-04-08T15-41-24Z-cpuv1',
    requestAutoCert: false,
    // FIXME: later
    // externalCertSecret: [
    //   {
    //     // Use certificates generated by cert-manager.
    //     name: (import 'sts-tls-certificate.jsonnet').spec.secretName,
    //     type: (import 'sts-tls-certificate.jsonnet').apiVersion,
    //   },
    // ],
    configuration: {
      // https://github.com/minio/operator/blob/e054c34ee36535b1323337816450dd7b3fcac482/helm/tenant/values.yaml#L75-L122
      name: minio_configuration.metadata.name,
    },
    users: [
      {
        name: minio_user0_secret.metadata.name,
      },
    ],
    buckets: [
      {
        name: bucket.minio_name,
      }
      for bucket in (import '../grafana-loki/_app_config.json').buckets
    ] + [
      {
        name: bucket_name,
      }
      for bucket_name in std.uniq(std.sort(std.objectValues((import '../grafana-mimir/_config.jsonnet').buckets)))
    ],
  },
};

[
  minio_user0_secret,
  minio_configuration,
  tenant,
]
